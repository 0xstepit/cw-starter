# Run tests
test:
  @echo 'ğŸ”¬ Running tests...'
  cargo test --locked
  @echo 'âœ… Tests completed.'

# Run clippy
clippy:
  @echo 'ğŸ” Running clippy...'
  cargo clippy --all-targets -- -D warnings
  @echo 'âœ… Clippy completed.' 

# Run fmt
fmt:
  @echo 'ğŸ“ Running fmt...'
  cargo fmt --all -- --check
  @echo 'âœ… Fmt completed.'

# Run checks required by github repo.
default-flow: fmt clippy test

# Run workspace optimizer
platform := if arch() =~ "aarch64" {"linux/arm64"} else {"linux/amd64"}
image := if arch() =~ "aarch64" {"cosmwasm/workspace-optimizer-arm64:0.12.12"} else {"cosmwasm/workspace-optimizer:0.12.12"}
optimize:
  @echo 'ğŸš€ Running build optimizer...'
  docker run --rm -v "$(pwd)":/code \
    --mount type=volume,source="$(basename "$(pwd)")_cache",target=/code/target \
    --mount type=volume,source=registry_cache,target=/usr/local/cargo/registry \
    --platform {{platform}} \
    {{image}}
  @echo 'âœ… Optimized build completed.'

schema:
  ./scripts/build_schema.sh